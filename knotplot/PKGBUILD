# Maintainer: erooke

pkgname=knotplot-bin
pkgver=1.0
pkgrel=1
pkgdesc="The World's most elaborate knotting software"
arch=('x86_64')
url="https://knotplot.com"
license=('proprietary')
depends=("freeglut" "glu")
makedepends=()
optdepends=("php: some demos use php" "tcsh: some demos use tcsh/csh")
provides=('knotplot')
conflicts=('knotplot')
options=()
source=("https://knotplot.com/download/Linux-Ubuntu/knotplot.gz" "https://knotplot.com/download/distribution/kpdist.tar.gz" )
sha256sums=('5ec9632835728bd23ab9d1e26e01698a161d1bcb7c6dd7a7283ab3a309d1175f' '3604d0cd97423297f57e363ae10620e3af0a957798f77972fe238a733e1dd338')

prepare(){
  cd "$srcdir"
  mv "FileList.txt" "FileList.txt.orig"
  comm <(find . -type f | sort) <(sort "FileList.txt.orig") -12 > "FileList.txt"
}

package() {
  install -Dm755 "knotplot" "${pkgdir}/usr/bin/knotplot"
  while read f;
  do
    install -Dm755 "${f}" "${pkgdir}/usr/share/knotplot/${f}"
  done < FileList.txt
}
