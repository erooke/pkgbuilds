pkgname=kind2-git
_pkgname=kind2
pkgver=v1.6.0.r86.g4417dc55
pkgrel=1
pkgdesc="SMT-based automatic model checker for safety properties of Lustre programs"
arch=('x86_64')
url="https://kind.cs.uiowa.edu/"
license=('Apache')
makedepends=('ocaml' 'dune' 'opam')
depends=( 'z3' )
provides=('kind2')
source=("git+https://github.com/kind2-mc/kind2")
sha256sums=('SKIP')


pkgver() {
  cd "$_pkgname"

  _tag=$(git tag -l --sort -v:refname | sed '/rc[0-9]*/d' | head -n1)
  _rev=$(git rev-list --count $_tag..HEAD)
  _hash=$(git rev-parse --short HEAD)
  printf "%s.r%s.g%s" "$_tag" "$_rev" "$_hash" | sed 's/^rel-//;s/-/./g'
}

build() {
  cd "$_pkgname"

  opam init --root=opam -n
  rm -rf _opam && opam switch --root=opam create -y .
  opam exec --root=opam dune build src @install

}

package() {
  cd "$_pkgname"
  mkdir -p "$pkgdir/usr/"
  dune install --sections=bin --prefix="$pkgdir/usr/"
}
